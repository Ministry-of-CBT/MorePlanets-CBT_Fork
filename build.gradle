buildscript {
    repositories {
        gradlePluginPortal()
        maven { url "https://files.minecraftforge.net/maven" }
        maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

def mcVersion = project.mc_version
def gcDevVersion = "1.12.2-4.0.2.261"

version = project.version
group = "stevekung.mods.moreplanets"
archivesBaseName = "More-Planets-All"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mcp_mapping}"
    makeObfSourceJar = false
    clientJvmArgs += "-Dfml.coreMods.load=micdoodle8.mods.miccore.MicdoodlePlugin -Dmixin.checks.interfaces=true -Dmixin.hotSwap=true -Dmixin.debug.export=true -Dmixin.debug.verbose=true -javaagent:E:/Development Tools/Gradle/caches/modules-2/files-2.1/org.spongepowered/mixin/0.8.4-SNAPSHOT/9d3e1c513ca669575aa25ddd530b379303ec0df8/mixin-0.8.4-SNAPSHOT.jar"
    clientRunArgs += "--username ${project.username} --uuid ${project.uuid} --mixin mixins.more_planets.json"

    replace "4.0.1.-1", project.gc_require_version
    replaceIn "MorePlanetsMod.java"
}

repositories {
	maven {
		name "SpongePowered"
		url "https://repo.spongepowered.org/repository/maven-public/"
	}
	maven {
		name "CurseMaven"
        url "https://www.cursemaven.com"
    }
    maven {
        name "CodeChickenLib"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "CurseForge"
        url "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name "JEI"
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "CTM"
        url "http://maven.tterrag.com/"
    }
    maven {
        name "Hwyla"
        url "https://maven.tehnut.info"
    }
    maven {
        name "Baubles"
        url "http://maven.thiakil.com/"
    }
    maven {
        name "Buildcraft"
        url "https://mod-buildcraft.com/maven/"
    }
    maven {
        name "AA"
        url "https://maven.ellpeck.de/"
    }
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
    }

    flatDir {
        dirs "libs"
    }
}

dependencies {
    compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    compile "mekanism:Mekanism-1.12.2:9.7.5.376:api"
    compile "com.github.glitchfiend.biomesoplenty:BiomesOPlenty:1.12.2-7.0.1.2441:api"
    compile "com.mod-buildcraft:buildcraft-api:7.99.24.6"
    compile "redstone-flux:RedstoneFlux-1.12:2.1.0.6:universal"
    compile "industrial-craft:industrialcraft-2-2.8.157:ex112:api"
    compile "player-api:PlayerAPI:1.12.1:1.0"
    compile "de.ellpeck.actuallyadditions:ActuallyAdditions_1.12.2:1.12.2-r151:api"
    compile "curse.maven:mixinBooter-419286:3321174"

    deobfCompile ":GalacticraftCore-Dev-" + gcDevVersion
    deobfCompile ":Galacticraft-Planets-Dev-" + gcDevVersion
    deobfCompile ":MicdoodleCore-Dev-" + gcDevVersion

    if (file("SteveKunG's-Lib-1.12.2-1.1.10-deobf.jar").exists()) {
        deobfCompile ":SteveKunG's-Lib-1.12.2-1.1.10-deobf"
    }

    deobfCompile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    deobfCompile "team.chisel.ctm:CTM:MC${ctm_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
    //deobfCompile "stevekungs-lib:SteveKunG's-Lib-1.12.2:1.1.10:deobf"
    deobfCompile "indicatia:Indicatia:1.12.2:1.3.3"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"

    compile("org.spongepowered:mixin:0.8.4-SNAPSHOT") {
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
    }
}

mixin {
	add sourceSets.main, "mixins.more_planets.refmap.json"
}

jar {
    manifest {
        attributes("FMLAT": "moreplanets_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "MixinConfigs": "mixins.more_planets.json",
                   "ForceLoadAsMod": "true"
        )
    }
    baseName = "More-Planets-" + mcVersion
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "261990"
            changelog = file("CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion mcVersion

            mainArtifact(jar) {
                displayName = "More-Planets-" + mcVersion + "-" + version
                relations {
                    requiredLibrary "stevekungs-lib"
                    requiredLibrary "mixin-booter"
                    optionalLibrary "jei"
                    optionalLibrary "ctm"
                    optionalLibrary "hwyla"
                    optionalLibrary "jeid"
                }
            }
        }
    }
}

tasks.curseforge.dependsOn(build)
jar.finalizedBy("reobfJar")