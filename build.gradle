buildscript {
    repositories {
        gradlePluginPortal()
        maven { url "https://maven.minecraftforge.net" }
        maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

def mcVersion = project.mc_version
String gcDevBuild = "261"
String gcDevVersion = "1.12.2-4.0.2.${gcDevBuild}"

version = project.version
group = "stevekung.mods.moreplanets"
archivesBaseName = "More-Planets-All"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mcp_mapping}"
    makeObfSourceJar = false
    clientJvmArgs += "-Dfml.coreMods.load=micdoodle8.mods.miccore.MicdoodlePlugin -Dmixin.checks.interfaces=true -Dmixin.hotSwap=true -Dmixin.debug.export=true -Dmixin.debug.verbose=true"
    clientRunArgs += "--username ${project.username} --uuid ${project.uuid} --mixin mixins.more_planets.json"

    replace "4.0.1.-1", project.gc_require_version
    replaceIn "MorePlanetsMod.java"
}

repositories {
    ivy {
        url "https://micdoodle8.com/new-builds/"
        layout 'pattern', {
            artifact "[organization]/${gcDevBuild}/[module]-[revision].jar"
        }
        metadataSources {
            artifact()
        }
    }
    maven {
        name "SpongePowered"
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        name "CurseMaven"
        url "https://www.cursemaven.com"
    }
    maven {
        name "CurseForge"
        url "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name "JEI"
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        name "Buildcraft"
        url "https://mod-buildcraft.com/maven/"
    }
    maven {
        name "AA"
        url "https://maven.ellpeck.de/"
    }
    maven {
        name "Modmaven"
        url "https://modmaven.dev/"
    }

    flatDir {
        dirs "libs"
    }
}

dependencies {
    // Galacticraft deps
    compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    compile "mekanism:Mekanism-1.12.2:9.7.5.376:api"
    compile "com.github.glitchfiend.biomesoplenty:BiomesOPlenty:1.12.2-7.0.1.2441:api"
    compile "com.mod-buildcraft:buildcraft-api:7.99.24.6"
    compile "curse.maven:redstoneflux-1.1.1-270789:2920436"
    compile "industrial-craft:industrialcraft-2-2.8.157:ex112:api"
    compile "curse.maven:playerapi-1.1-449521:3212872"
    compile "de.ellpeck.actuallyadditions:ActuallyAdditions_1.12.2:1.12.2-r151:api"

    compile "GC-1.12-dev:GalacticraftCore-Dev:${gcDevVersion}"
    compile "GC-1.12-dev:Galacticraft-API:${gcDevVersion}"
    compile "GC-1.12-dev:Galacticraft-Planets-Dev:${gcDevVersion}"
    compile "GC-1.12-dev:MicdoodleCore-Dev:${gcDevVersion}"

    // More Planets deps
    if (file("SteveKunG's-Lib-1.12.2-${lib_version}-deobf.jar").exists()) {
        deobfCompile ":SteveKunG's-Lib-1.12.2-${lib_version}-deobf"
    }
    else {
        deobfCompile "curse.maven:stevekungs-lib-296019:3592435"
    }

    deobfCompile "curse.maven:mixinBooter-419286:3575119"
    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    deobfCompile "curse.maven:codechicken-lib-1-8-242818:2779849"
    deobfCompile "curse.maven:hwyla-253449:2568751"
    deobfCompile "curse.maven:baubles-227083:2518667"

    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    runtime "curse.maven:indicatia-272146:2854909"
    runtime "curse.maven:ctm-267602:2915363"

    compile "org.spongepowered:mixin:0.8.3"
    annotationProcessor "org.spongepowered:mixin:0.8.3:processor"
}

sourceSets {
    main {
        ext.refMap = "mixins.more_planets.refmap.json"
    }
}

jar {
    manifest {
        attributes("FMLAT": "moreplanets_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "MixinConfigs": "mixins.more_planets.json",
                   "ForceLoadAsMod": "true"
        )
    }
    baseName = "More-Planets-" + mcVersion
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "261990"
            changelog = file("CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion mcVersion

            mainArtifact(jar) {
                displayName = "More-Planets-" + mcVersion + "-" + version
                relations {
                    requiredLibrary "stevekungs-lib"
                    requiredLibrary "mixin-booter"
                    optionalLibrary "jei"
                    optionalLibrary "ctm"
                    optionalLibrary "hwyla"
                    optionalLibrary "jeid"
                }
            }
        }
    }
}

tasks.curseforge.dependsOn(build)
jar.finalizedBy("reobfJar")